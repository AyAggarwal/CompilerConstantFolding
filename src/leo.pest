// Grammar rules for a small subset of the Leo programming language.
// Example:
// function main() {
//     let a = 1u8 + 1u8;
// }

// program
program = _{ SOI ~ function_header ~ "\n"* ~ (statement ~ "\n"+) * ~ statement? ~ "}" ~ EOI }


// functions
function_header = { "function" ~ ident ~ "(" ~ input* ~ ")" ~ "{" }

input = { ident ~ ":" ~ value_type }


// statements
statement = { assign ~ ";" | branchif ~ "\n"}

assign = { "let" ~ ident ~ "=" ~ expression }

branchif = {"if" ~ expression ~ "{" ~ "\n"* ~ codebundle ~ "}" ~ "else" ~ "{" ~ "\n"* ~ codebundle ~ "}" }

codebundle = {(statement ~ "\n"+)* ~ statement?}


// expressions
expression = { binary | value }

binary = { value ~ operator ~ expression }

// operators
operator = {
    "+" | "-" | "*"  | "/" | ">" | "<" | "=="
}


// values
value = _{  integer | boolean | ident | "(" ~ expression ~ ")" }

value_type = { "u8" }

integer = @{ "_"? ~ ASCII_DIGIT+ ~ value_type }

boolean = {"true" | "false"}

ident = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }


// misc
WHITESPACE = _{ " " | "\t" }
COMMENT = _{ "NB." ~ (!"\n" ~ ANY)* }
